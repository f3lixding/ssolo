{
  description = "ssolo flake";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs = { self, nixpkgs, flake-utils }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};

        sokol-shdc = pkgs.stdenv.mkDerivation {
          name = "sokol-shdc";
          version = "unstable";

          src = pkgs.fetchurl {
            url =
              "https://github.com/floooh/sokol-tools-bin/raw/master/bin/linux/sokol-shdc";
            sha256 = "1qpm8rp1dgidllq563njwrjrg1w2nk9j4akvjc0qhjhxqj60pi8x";
          };

          dontUnpack = true;

          installPhase = ''
            mkdir -p $out/bin
            cp $src $out/bin/sokol-shdc
            chmod +x $out/bin/sokol-shdc
          '';
        };

        buildInputs = with pkgs; [
          zig
          sokol-shdc
          libx11
          libxi
          libxcursor
          libGL
          alsa-lib
        ];
      in {
        # Dependencies are now prefetched via build.zig.zon.nix generated by zon2nix
        packages.default = let
          zigDeps = import ./build.zig.zon.nix {
            inherit (pkgs) lib linkFarm fetchurl fetchgit runCommandLocal;
            inherit (pkgs) zig;
            name = "zig-packages";
          };
        in pkgs.stdenv.mkDerivation {
          name = "ssolo";
          version = "0.0.0";
          src = ./.;

          nativeBuildInputs = with pkgs; [ zig_0_15 sokol-shdc pkg-config ];

          buildInputs = with pkgs; [ libx11 libxi libxcursor libGL alsa-lib ];

          preBuild = ''
            export ZIG_GLOBAL_CACHE_DIR=$PWD/zig-cache

            # Set up Zig dependency cache
            mkdir -p $ZIG_GLOBAL_CACHE_DIR/p
            find ${zigDeps} -maxdepth 1 -type l | while read dep; do
              ln -sf $(readlink "$dep") "$ZIG_GLOBAL_CACHE_DIR/p/$(basename "$dep")"
            done
          '';

          buildPhase = ''
            runHook preBuild
            zig build -Doptimize=ReleaseSafe --prefix $out --prefix-exe-dir bin
            runHook postBuild
          '';

          installPhase = ''
            # Installation is handled by the build phase
            runHook preInstall
            runHook postInstall
          '';

          meta = with pkgs.lib; {
            description = "ssolo game";
            platforms = platforms.linux;
          };
        };

        devShells.default = pkgs.mkShell {
          inherit buildInputs;

          shellHook = ''
            exec ${pkgs.zsh}/bin/zsh
          '';
        };
      });
}
